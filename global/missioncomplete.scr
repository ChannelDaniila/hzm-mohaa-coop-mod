//chrissstrahl - modified original file for loading next map
//2018.07.15

main local.nextlevel local.bsp local.skipFade:{
	local.threadName = "global/missioncomplete.scr::main"			//[200] Smithy - store the thread name

	//chrissstrahl - make sure this only is done in sp
	if(	level.gametype == 0 && $player.health <= 0 ){
		pause
	}
	
	if (local.skipFade != 1){
		local.skipFade = 0
	}

	//chrissstrahl - stop watch for all players
	exec coop_mod/replace.scr::stopwatch 0
	
	if (local.nextlevel != NIL)
	{
		//[200] chrissstrahl - set nect map to be active map for dedicated server reboot
		if(level.gametype != 0 && level.coop_dedicated != 0){
			setcvar "ui_startmap" local.nextlevel
			setcvar "coop_prevMapList" (getcvar sv_maplist) //[200] Smithy - used to reload sv_maplist on next load
			setcvar "sv_maplist" "" 						//[200] Smithy - so our coop mission transition works
		}
	
		level.coop_preventGameTypeChanges = game.true 				//[200] Smithy - stop any further changes via main.scr::changeGameType if there are any
		if (level.coop_changeGameTypeThread){  						//[200] Smithy - stop the thread if one is running					
			thread coop_mod/main.scr::changeGameTypeDebug ("(" + local.threadName + "): Terminating " + level.coop_changeGameTypeThread.threadName + " thread for level change, and stopping any future calls.")
			level.coop_changeGameTypeThread end
		}

		//[200] Smithy - probably doesn't matter but to be safe
		if (level.gametype != 0){
			local.gametype = getcvar( "g_gametype" )
			setcvar "g_gametype" "0"	
		}
		
		if (local.bsp != NIL)
		{
			game.loadout = false
			bsptransition local.nextlevel local.skipFade
		}
		else
		{
			game.loadout = NIL
			leveltransition local.nextlevel local.skipFade
		}
		
		//[200] Smithy - probably doesn't matter but to be safe
		if (level.gametype != 0){
			setcvar "g_gametype" local.gametype	
		}
	}
}end
