TIKI

setup
{
	scale 0.52
        path models/weapons/enfieldsniper
	skelmodel Uk_W_enfieldsniper.skd
	surface material1 shader Uk_W_L42A1
	surface material2 shader Uk_W_L42A1
	surface material3 shader Uk_W_L42A1
	surface material4 shader lens
}

init
{
	server
		{
	//========================================//
	//		WEAPON NAME		  //
	//========================================//

		classname		Weapon
		weapontype		rifle
		name			"Enfield L42A1"
		rank			260 260

	//========================================//
	//		WEAPON ACCURACY 	  //  Higher/Better Lower/Suckier (dj)
	//========================================//

		sp bulletrange		4000	 	//the range at which bulletspread is applied
		sp bulletspread		35 35 150 150 	//minpitch minyaw maxpitch maxyaw
		sp zoomspreadmult	0.055			//bulletspread accuracy boost multiplier when zoomed

		dm bulletrange		4000
		dm bulletspread		59 59 300 300 
		dm zoomspreadmult	0.055

		realism bulletrange	4000
		realism bulletspread	59 59 300 300 
		realism zoomspreadmult	0.055

	//========================================//
	//		DAMAGE / RATE OF FIRE	  //
	//========================================//

		sp bulletdamage		75
		dm bulletdamage		100

		sprealism bulletdamage	100	
		dmrealism bulletdamage	125

		sp firedelay 		1.2	
		dm firedelay		1.1	
		realism firedelay	1.1	

		realism throughwood	24
		realism throughmetal	8

	//========================================//
	//		PLAYER MOVEMENT		  //
	//========================================//

		sp movementspeed		0.94
		sp zoommovement			0.5

		dm movementspeed		0.95
		dm zoommovement			0.5

		realism movementspeed		0.95
		realism zoommovement		0.5



	//========================================//
	//	RIFLE BUTT / SECONDARY FIRE       //
	//========================================//


	//========================================//
	//		AMMO			  //
	//========================================//

		firetype		bullet
		ammotype		"rifle"
		meansofdeath	        bullet
		semiauto
		bulletcount		1
		clipsize		10
		ammorequired	        1
		bulletlarge		1

		sp startammo	        30
		dm startammo	        20
		sprealism startammo	25
		dmrealism startammo	20

		// Additional DM Loadout
		dm additionalstartammo "grenade"      1
		dm additionalstartammo "smokegrenade" 1
		dm startitem "items/binoculars.tik"

		// Additional DM Realism Loadout
		dmrealism additionalstartammo "grenade"      1
		dmrealism additionalstartammo "smokegrenade" 1
		dmrealism startitem "items/binoculars.tik"

	//========================================//
	//	CROSSHAIR, ZOOM, TRACERS  	  //
	//========================================//

		sp tracerfrequency 		2 
		dm tracerfrequency 		0
		sprealism tracerfrequency 	0 
		dmrealism tracerfrequency 	0

		crosshair		        1
                zoom			        20

	//========================================//
	//	SOUND, AI, OTHER	  	  //
	//========================================//

		pickupsound		enfieldsniper_snd_pickup
		ammopickupsound		enfieldsniper_snd_pickup_ammo
		noammosound		enfieldsniper_snd_noammo

		// AI animation group info
		weapongroup		rifle
		airange			sniper

		// this is attached to the player during reload
		cache models/weapons/Uk_W_L42A1_clip.tik

		//[200] Smithy - get handled
		exec coop_mod/itemhandler.scr "initialiseItem" "rifle" "Enfield L42A1"
	}
	client
	{
		cache tracer.spr
		cache muzsprite.spr
		cache models/fx/snipesmoke.tik
		cache models/ammo/rifleshell.tik
	}
}

animations
{
	idle		Uk_W_enfieldsniper.skc
	{
	}

	reload		A_W_enfieldsniper_reload_W.skc      
	{
		client
		{
			entry sound enfieldsniper_snd_reload item
			87 sound enfieldsniper_snd_bolt item
		}
		server
		{
	 		//frames are 30, 62
			30 surface material3	+nodraw //  1/2 first frame
			62 surface material3    -nodraw //  last frame
			last idle
		}
	}
	rechamber	A_W_enfieldsniper_rechamber_W.skc
	{
		client
		{
			entry sound enfieldsniper_snd_bolt item
			16 tagspawn tag_eject 1024
			(
				spawnrange 2048
				count 1
				model models/ammo/rifleshell.tik
				scale 1.0
				velocity 70
				randvel crandom 10 crandom 10 random 20
				emitterangles 0 0 0
				avelocity crandom 90 crandom 90 0
				accel 0 0 -800
				physicsrate 20
				life 2.0
				fadedelay 1.7
				collision
				bouncefactor 0.2
				bouncesoundonce snd_rifle_shell
			)
		}
	}
	fire		A_W_enfieldsniper_fire_W.skc
	{
		server
		{
			entry shoot
		}
		client
		{
			entry stopaliaschannel enfieldsniper_snd_fire
			entry sound enfieldsniper_snd_fire

			// By now, the server has already fired the weapon, so it's
			// ok to apply the view kick apon entry to avoid problems
			// with multiple application on single frame animations.

			////////////////////////////////////////////////////////////////////////////////////////
			// View Kicking
			//
			// View Kicking works based on the assumptions that every weapon has its own tendencies to kick in
			// a paticular fashion. In MOH we call then scatter patterns. Currently we have 2 scatter patterns which
			// will be explained.
			// Scatter Patterns:
			// "V" - the cone. The longer you shoot the more random your shots will be in the horizontal axis.
			// "T" - the T shape. The gun has a tendancy to push in a paticular direction.
			//
			//
			//             	+-------------------------------------------- Scatter Pitch Min
			//              |  	+--------------------------------------- Scatter Pitch Max
			//           	|    	|       +------------------------------- Scatter Yaw Min
			//             	|    	|       |   +--------------------------- Scatter Yaw Max
			//             	|    	|       |   |     +--------------------- The Recentering speed in fraction per second
			//              |   	|       |   |     |    +---------------- The Scatter Pattern
			//             	|    	|       |   |     |    |     +---------- The absolute pitch min/max
			//             	|    	|       |   |     |    |     |  +------- The absolute yaw min/max
			//             	|    	|       |   |     |    |     |  | +----- This is the pitch at which you loose all
			//             	|    	|       |   |     |    |     |  | |      control of the weapon and its behavior is
			//             	|    	|       |   |     |    |     |  | |      purely random.
			//		V    	V       V   V     V    V     V  V V
			entry viewkick -1.3   -1.3    -0.3 -0.3   1   "T"    5 0.5 5

			// muzzle flash
			entry tagdlight tag_barrel 0.25 0.2 0.15 150 0.11
			entry tagspawnlinked tag_barrel
			(
				model muzsprite.spr
				count 1
				alpha .5
				color 1.00 1.00 1.00
				varycolor
				scale 0.70
				life 0.05
				scalerate 1.00
				velocity 4.00
				friction 3.00
				fade
				alignstretch 1.00
				randomroll
			)
			entry tagspawnlinked tag_barrel
			(
				model muzsprite.spr
				count 1
				alpha .5
				color 1.00 1.00 1.00
				scale 0.60
				life 0.05
				scalerate 0.70
				velocity 17.00
				offsetalongaxis 5 0 0
				fade
				alignstretch 0.40
				randomroll
			)

			entry tagspawnlinked tag_barrel
			(
				model muzsprite.spr
				count 1
				alpha .5
				color 1.00 1.00 1.00
				scale 0.60
				life 0.05
				velocity 18.00
				offsetalongaxis 9 0 0
				fade
			)

			// smoke puff used on sniper version of kar98
			entry commanddelay 0.020 tagspawn tag_barrel
			(
				model vsssource.spr
				count 1
				alpha 0.10
				color 1.00 1.00 1.00
				spritegridlighting
				scale 0.09
				life .9
				scalerate 23.00
				velocity 429.00
				accel 0.00 20.00 30.00
				friction 2.00
				fade
				offsetalongaxis random 4 0 0
			)

			entry tagspawn tag_barrel
			(
				count 1
				scale .7
				model models/fx/snipesmoke.tik
			)
		}
	}
}

/*QUAKED addon_weapon_L42A1 (0.0 0.0 0.0) (-65 -40 0) (65 40 120) L42A1
*/	


