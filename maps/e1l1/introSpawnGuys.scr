//
// introSpawnGuys.scr
//

spawnGuys:
	//chrissstrahl - fix strecher guys out of sync
	//chrissstrahl - trying to fix strecher guys out of sync
	level.strecherfixtime = 1
	
	// setup
	level.intro_model[1] = models/human/Sc_AL_US_infmask.tik
	level.intro_model[2] = models/human/Sc_AL_US_inf.tik
	level.intro_model[3] = models/human/Sc_Al_US_med.tik
	spawn trigger_once targetname "stretcherGo"

	if (level.skipJeepRide || level.warpToFrontLine)
		end

	//
	// specialized animations, attached to spider points
	//
	//              model		origin_tag				anim						loop		holster		anim_delay (none == auto)
	thread spiderGuy 	1 		sentinel_start 	   		A_11_sentinel1 				0 			0 			4

	thread spiderGuy 	2 		streetcrosser2_start	A_11_streetcrosser2_walk	0 			1 			2

	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype != 0 || level.slowMachine == 0) {
	thread spiderGuy 	2 		maproom 				A_11_mapRoomA 				0 			1 			15		
	thread spiderGuy 	2 		maproom 				A_11_mapRoomB 				0 			1 			15 
	thread spiderGuy 	2 		maproom 				A_11_mapRoomC 				0 			0 			15 
	}

	thread spiderGuy 	1 		mp_start 				A_11_jeepstop 				0 			1 			11.5
	//chrissstrahl - trying to fix strechter bitches
	if(level.gametype == 0 ){
		thread spiderGuy 	3 		stretcher_start			A_11_stretcherA				0 			1 			9
		thread spiderGuy 	3 		stretcher_start			A_11_stretcherB				0 			1 			9
		thread spiderGuy 	1 		stretcher_start			A_11_stretcherC				0 			1 			9	
	}else{
		thread spiderGuy 	3 		stretcher_start			A_11_stretcherA				0 			1 			5
		thread spiderGuy 	3 		stretcher_start			A_11_stretcherB				0 			1 			5
		thread spiderGuy 	1 		stretcher_start			A_11_stretcherC				0 			1 			5
	}

	thread spiderGuy 	1 		woundedknee_start		A_11_woundedknee_idle		0 			1 						  
																													  
	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype != 0 || level.slowMachine == 0) {
	thread spiderGuy 	3 		cots					A_11_cot_medic				0 			1 			 			  
	thread spiderGuy 	1 		cots					A_11_cot_injured   			0 			1 			 			    	
	}

	thread spiderGuy 	2 		dazed_start				A_11_dazed   				0 			1 			 
	thread spiderGuy 	2		stumblehelper_start		A_11_stumblehelper			0			1		

	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype != 0 || level.slowMachine == 0) {
	thread spiderGuy 	1 		injuredcarry_start		A_11_injuredcarryA   		0 			1 			 
	thread spiderGuy 	2 		injuredcarry_start		A_11_injuredcarryB   		0 			1 			 
	}
	thread spiderGuy 	2 		headwound_start			A_11_headwoundA   			0 			1 			 
	thread spiderGuy 	2 		headwound_start			A_11_headwoundB   			0 			1 			 

	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype != 0 || level.slowMachine == 0) {
	thread spiderGuy 	1		injuredtriplet_start	A_11_injuredtripletA		0			1			33
	thread spiderGuy 	1		injuredtriplet_start	A_11_injuredtripletB		0			1			33
	thread spiderGuy 	1		injuredtriplet_start	A_11_injuredtripletC		0			1			33
	thread spiderGuy 	1 		thumbsupguy_start		A_11_thumbsupguy   			0 			1 			33
	}


	//
	// walkers
	//
	// rifle anims from models/human/animation/human_rifle.tik
	//				model	path					upper-anim				lower-anim						speed	holster	delay 	delete		gun
	thread walker 	1 		jogger1_point_1 		rifle_stand_surprise 	rifle_walk_patrol_forward 	 	40 		0 	   	1.2 
	thread walker 	2 		jogger1_point_2 		m1garand_reload 		rifle_walk_patrol_forward	 	60 		0 	   	0.6 	   	 
	thread walker 	2 		jogger1_point_3 		kar98_hipaim 			rifle_walk_patrol_forward 		60 		0 	   	0  	  
	thread walker 	2 		intro_limpguy1 			NULL 					A_11_InjuredWalk02 	 			60 		1 	   	15 


	// runner guys
	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype != 0 || level.slowMachine == 0) {
	thread walker 	1 		intro_runner_1 			NULL 					thompson_run_forward 	 		300 	0 	   	13.5 		1			thompson
	thread walker 	1 		intro_runner_2 			NULL 					thompson_run_forward 	 		300 	0 	   	13.5 		1			thompson
	thread walker 	1 		intro_runner_3 			NULL 					thompson_run_forward 	 		300 	0 	   	22	 		1			thompson
	thread walker 	1 		intro_runner_3 			NULL 					thompson_run_forward 	 		300 	0 	   	23	 		1			thompson
	}

	local.w = 34
	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype != 0 || level.slowMachine == 0) {
	thread tankFollower 	1 		1	intro_tankFollower_1 	(local.w+4)	 		8  5 intro_tankFollowerFinish_a
	thread tankFollower 	1 		2	intro_tankFollower_2 	(local.w+7)	 		8  5 intro_tankFollowerFinish_b
	thread tankFollower 	1 		3	intro_tankFollower_3 	(local.w+8)	 		8  5 intro_tankFollowerFinish_c
	thread tankFollower 	1 		4	intro_tankFollower_4 	(local.w+10)	 	8  5 intro_tankFollowerFinish_f
	}
	thread tankFollower 	1 		5	intro_tankFollower_5 	(local.w+10.5) 		8  5 intro_tankFollowerFinish_e
	thread tankFollower 	1 		6	intro_tankFollower_6 	(local.w+12.25)		8  5 intro_tankFollowerFinish_g


	//
	// idle guys
	//
	//				model	attach_node				angle	anim						holster		delay		Z-Adj	gun
	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype != 0 || level.slowMachine == 0) {
		thread idleGuy	2 		intro_talking_1 		0		A_11_conversationA 			1 	   
		thread idleGuy	2 		intro_talking_1 		0		A_11_conversationB 			1 	   
		thread idleGuy	2 		intro_squatguy 			0		unarmed_squat		 		1 	   
		thread idleGuy	2 		intro_truckwatch_1 		0		A_11_truck_watch_guy02 		1 	   	NULL		10
		thread idleGuy	2 		intro_truckwatch_2 		240		A_11_truck_watch_guy03 		1 	   	NULL		20
		thread idleGuy	1 		intro_mgunner_idle 		45		thompson_stand_nervous_a 	0		NULL 	   	0		thompson
		thread idleGuy	1 		intro_mgunner_idle_2 	0		thompson_stand_nervous_b 	0		NULL 	   	0		thompson
	}

	//
	// special/custom gags
	//
	thread tankFixers
	thread jeepCrash
	thread crateGuy
	thread streetCrosser
	thread stretcher
 	thread trucks
	thread loadedTruck

	//chrissstrahl - we don't do this in coop
	if (level.gametype == 0 && level.slowMachine != 0) { //1 or otherwhise positive(slow)
		// if we're on a slow machine, get rid of unnecessary stuff immediately
		wait 1
		local.n = $scene1Cleanup.size
		for (local.i = 1; local.i <= local.n; local.i++)
		{
			$scene1Cleanup[local.i] remove
		}
		$introTank_1 remove
	}
end

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

crateGuy:
	local.guy = spawn models/human/Sc_AL_US_inf.tik
	local.guy.origin = $intro_crate_guy.origin
	local.guy.angles = ( 0 0 0 )
	local.guy holster

	local.guy anim A_11_crate_pickup1
	wait 1.3
	local.guy ai_off
	wait 6.5
	local.guy ai_on
	local.guy waittill animdone

	local.guy anim A_11_crate_pickup2
	local.guy waittill animdone

	local.guy anim crate_walk

	wait 2
	local.a = 0
	while (local.a > -45) {
		local.guy.angles = ( 0 local.a 0 )
		local.a -= 2
		waitframe
	}

	wait 10
	local.guy anim crate_putdown1
	local.guy waittill animdone

	local.guy anim crate_putdown2
	local.guy waittill animdone

	local.guy remove
end

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

truckInit:
	local.model = models/human/Sc_AL_US_inf.tik

	local.origin = self.origin
	local.angles = self.angles
	self.origin = ( 0 0 0 )
	self.angles = ( 0 0 0 )

	self.guy = spawn local.model
	self.guy physics_off
	self.guy notsolid
	self.guy holster
	self.guy.origin = self.origin
	self.guy.origin += ( 45 10 78 )
	self.guy.angles = self.angles
	self.guy bind self
	self.guy anim 00A004_driversitstill
	
	self.origin = local.origin
	self.angles = local.angles
end

trucks:
	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype == 0 && level.slowMachine != 0) {
		$introTruck_1 remove
		$introTruck_2 remove
		end
	}

	$jeepRideStart waittill trigger	
	wait 24

	$introTruck_1 waitthread truckInit
	$introTruck_1 drive $introTruck_1_path 150 75 200 200

	$introTruck_2 waitthread truckInit
	$introTruck_2 drive $introTruck_2_path 150 75 200 200
	$introTruck_1 waittill drive

	$introTruck_1.guy remove
	$introTruck_1 remove
	$introTruck_2.guy remove
	$introTruck_2 remove
end

loadedTruck:
	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype == 0 && level.slowMachine != 0) {
		$introTruck_3 remove
		end
	}

	$jeepRideStart waittill trigger	
	wait 26
	$introTruck_3 waitthread truckInit

	local.model = models/human/Sc_AL_US_infmask.tik
	local.guy0 = spawn local.model
	local.guy0 rendereffects "-shadow"
	local.guy0 physics_off
	local.guy0 notsolid
	local.guy0 holster
	local.guy0.origin = $intro_getInTruckGuy.origin
	local.guy0.angles = ( 0 -90 0 )
	local.guy0.origin += ( -25 100 70 )
	local.guy0 thread global/loopanim.scr::LoopAnim unarmed_squat

	local.guy = spawn local.model
	local.guy rendereffects "-shadow"
	local.guy.origin = $intro_getInTruckGuy.origin
	local.guy.origin += ( 0 -25 20 )
	local.guy.angles = ( 0 90 0 )
	wait 10.5
	local.guy anim_noclip A_11_Geton_GMC
	local.guy ai_on
	local.guy waittill animdone
	local.guy ai_off
	wait 3
	local.guy thread global/loopanim.scr::LoopAnimStop
	local.guy remove
	local.guy0 remove
	wait 5

	$introTruck_3 remove
	$introTruck_3.guy remove

	//$introTruck_3 drive $introTruck_3_path 150 75 200 200
	//$introTruck_3 waittill drive
end

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

streetCrosser:
	$jeepRideStart waittill trigger	
	wait 2
	local.model = models/human/Sc_AL_US_inf.tik
	local.guy = spawn local.model
	local.guy.origin = $introLocations gettagposition streetcrosser1_start
	local.guy holster
	local.guy anim_noclip A_11_streetcrosser1_walk
	//local.guy waittill animdone
	wait 7.5
	local.guy thread global/loopanim.scr::LoopAnim A_11_streetcrosser1_idle
	wait 9
	local.guy thread global/loopanim.scr::LoopAnimStop
	local.guy remove
end


tankFixers:
	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype == 0 && level.slowMachine != 0){
		end
	}

	$jeepRideStart waittill trigger	
	wait 26
	local.model = models/human/Sc_AL_US_inf.tik

	local.guy_1 = spawn	local.model
	local.guy_2 = spawn	local.model
	local.guy_3 = spawn	local.model

	local.guy_1 thread tankFixerGuy A_11_mechanic1	
	local.guy_2 thread tankFixerGuy A_11_mechanic2	
	local.guy_3 thread tankFixerGuy A_11_mechanic3	
end

tankFixerGuy local.anim:
	self rendereffects "-shadow"
	self nodamage
	self notsolid
	self.angles = $introTank_1[1].angles
	self.origin = $introTank_1[1].origin
	self thread global/loopanim.scr::LoopAnim local.anim
	wait 18
	self waitthread global/loopanim.scr::LoopAnimStop
	self remove
end

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

stretcher:
	//chrissstrahl - FINALLY !!!!!!!!!!!!! Fixing strecher guys out of sync
	while(level.strecherfixtime != 0){
		waitframe
	}
	wait 1.3 //1.2

	/*
	$jeepRideStart waittill trigger	
	$stretcherGo waittill trigger
	//chrissstrahl - trying to fix strecher guys out of sync
	while(level.strecherfixtime > 0){
		level.strecherfixtime-=0.1
		wait 0.1
	}
	*/
	
	//wait 0.8 //chrissstrahl
	spawn script_model model models/static/af_p_stretcher.tik targetname stretcher
	$stretcher notsolid
	$stretcher physics_off
	$stretcher.origin = $introLocations gettagposition stretcher_start
	waitframe
  	$stretcher moveanim A_11_stretcher_move
	//iprintlnbold_noloc("strechertrigger moving") //chrissstrahl - dev info
	$stretcher waittill animdone
	wait 10
	$stretcher remove
end

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

doJeepSound:
	wait 5.5
	$jeep2 playsound Intro_Jeep_NPC1
	wait 14.5
	$jeep2 playsound Intro_Jeep_NPC2
	wait 5.3
	$jeep2 playsound Intro_Jeep_NPC3
end

jeepCrash:
	$jeepRideStart waittill trigger	
	wait 20.5
	spawn script_model model models/vehicles/willys.tik targetname jeep2
	$jeep2 anim idlewindscreen_up

	$jeep2 notsolid
	$jeep2 physics_off

	wait 0.25
	$jeep2.origin = $introLocations gettagposition jeep2_start

	local.model = models/human/Sc_AL_US_infmask.tik
	local.guy_1 = spawn	local.model
	local.guy_1 holster
	local.guy_2 = spawn	local.model
	local.guy_2 holster
	local.guy_1 thread jeep2Guy A_11_jeep2driver
	local.guy_2 thread jeep2Guy A_11_jeep2passenger1

	//chrissstrahl - we don't do slow checks in coop
	if (level.gametype != 0 || level.slowMachine == 0) {
		local.guy_3 = spawn	local.model
		local.guy_4 = spawn	local.model
		local.guy_3 thread jeep2Guy A_11_jeep2passenger3
		local.guy_4 thread jeep2Guy A_11_jeep2passenger4
	}

	$jeep2 anim A_11_jeep2_drive_ready
	wait 0.1
	$jeep2 pauseanims 1
	wait 7.9
	$jeep2 pauseanims 0
	$jeep2 moveanim A_11_jeep2_drive
	thread doJeepSound

	wait 21.5
	local.ent = spawn script_origin
	local.ent.origin = $introLocations gettagposition emitter_explosion
	local.ent playsound arty_leadin05 
	wait 1
  	thread maps/e1l1/explosion.scr::big local.ent 
	thread bigLightFlash local.ent.origin //chrissstrahl - currected datatype, was entity should be origin
	local.ent playsound arty_exp_sand10
	wait 15
	local.ent remove

	// wait for player to look away from jeep, "cansee" wasn't working for me
	while (1) {
		//chrissstrahl - sp handle keep
		if( level.gametype == 0 ){
			local.toPlayer = $player.origin - $jeep2.origin
			local.playerDir = angles_toforward $player.viewangles
			if ((vector_dot local.toPlayer local.playerDir) > 0.0){ break }
		}
		//chrissstrahl - mp handle new for coop
		else{ 
			local.theyCantSee = 1
			for (local.i = 1;local.i <= $player.size;local.i++){
				local.player = $player[local.i]
				if( local.player == NULL ){ continue }
				local.jeepOrigin = $jeep2.origin
				local.toPlayer = local.player.origin - local.jeepOrigin
				local.playerDir = angles_toforward local.player.viewangles
				if ((vector_dot local.toPlayer local.playerDir) > 0.0){ break }
			}
			if(local.theyCantSee == 1){ break }
		}
		
		wait 0.5
	}
	$jeep2 remove
end

bigLightFlash local.origin:
	wait 1
	local.origin += ( 0 0 500 )
	thread maps/e1l1/explosion.scr::pocketFlashFx models/fx/fx_pocketflash_huge.tik local.origin
end

jeep2Guy local.anim:
	self rendereffects "-shadow"
	self nodamage
	self notsolid
	self.angles = $jeep2.angles

	self.origin = $jeep2.origin
	self glue $jeep2
	self anim_noclip local.anim
	wait 0.5
	self ai_off
	wait 7.4
	self ai_on
	self anim_noclip local.anim
	self waittill animdone
	self bedead
	wait 5
	self remove
end

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

spiderGuy local.modelNo local.originTag local.animName local.loop local.holster local.animDelay:
	if (local.loop == NIL)
		local.loop = 1
	if (local.holster == NIL)
		local.holster = 1
	local.useAnimDelay = 1
	if (local.animDelay == NIL) {
		// if no anim delay was defined, just start the animation as soon as
		//    the model is visible to the jeep
		local.animDelay = 99999
		local.useAnimDelay = 0
	}

	local.origin = $introLocations gettagposition local.originTag
	if (local.origin == NIL || local.origin == NULL) {
		println "ERROR ERROR ERROR ERROR ERROR ERROR ERROR" 
		println "ERROR ERROR ERROR ERROR ERROR ERROR ERROR"
		println "  can't find key location '" local.originTag "'"
		end
	} 

	// wait for the jeep to be within the far plane
	$jeepRideStart waittill trigger
	local.animTime = level.jeepRideStartTime + local.animDelay
	while (1) {	
		if (vector_within local.origin $jeep.origin 1500)
			break
		if (level.time >= local.animTime)
			break
		wait 0.2
	}
		
	local.guy = spawn level.intro_model[local.modelNo]  
	local.guy.enableEnemy = 0
	if (local.holster)
		local.guy holster

	local.guy thread spiderGuyThink local.origin local.animName local.loop local.animDelay local.useAnimDelay
	
	//chrissstrahl - trying tio fix strecher guys out of sync
	//level.strecherfixtime = local.animDelay
end

spiderGuyThink local.origin local.animName local.loop local.animDelay local.useAnimDelay:
	// snap into position, start animation
	self notsolid
	//self rendereffects "-shadow"
	self.origin = local.origin

	if (local.loop)
		self thread global/loopanim.scr::LoopAnim local.animName
	else 
		self anim_noclip local.animName

	if (local.animName == "A_11_stretcherA" || local.animName == "A_11_stretcherB" || local.animName == "A_11_stretcherC") {
		// don't use the anim delay for the stretcher guys, they will get out of sync with the stretcher
		trigger $stretcherGo
	}
	else if (local.useAnimDelay)	{
		// calculate delay, subtract off the time that the jeep has already
		//    been driving
		local.delay = local.animDelay - (level.time - level.jeepRideStartTime)

		// if there is a delay, then wait until we've struck a pose from the
		//    animation before freezing
		wait 0.1
	
		// now wait however long is left in the delay
		if (local.delay > 0) {
			self ai_off
			wait local.delay
			self ai_on
			
			//chrissstrahl - fix strecher fuck
			level.strecherfixtime = 0
		}	
	}

	if (!local.loop) {
		// when the animation is over, freeze in place
		self waittill animdone
		self ai_off
	}

	//[202] chrissstrahl - added check for self, to prevent spam
	// wait for the jeep to get past the far plane, then delete ourself
	while (self && $jeep && vector_within self.origin $jeep.origin 1500){
		wait 0.2
	}
	if( self ){ self remove }
end
	
//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

walker local.modelNo local.path local.upperAnim local.anim local.speed local.holster local.delay local.deleteOnEnd local.gun:
	$jeepRideStart waittill trigger	
	if (local.delay == NULL || local.delay == NIL) {
		// wait until the jeep is within range
		while (!(vector_within $(local.path).origin $jeep.origin 1500))
			wait 0.2
	}
	else if (local.delay > 0)
		wait local.delay

	local.guy = spawn level.intro_model[local.modelNo]  
	//local.guy rendereffects "-shadow"
	local.guy notsolid
	local.guy physics_off
	local.guy.origin = $(local.path).origin
	if (local.holster)
		local.guy holster
	else if (local.gun != NULL && local.gun != NIL)
		local.guy gun local.gun

	local.model = spawn script_model
	local.model.origin = $(local.path).origin
	local.model notsolid

	local.guy bind local.model

	//local.anim = "A_11_InjuredWalk02"
	if (local.anim != NULL)
		local.guy thread global/loopanim.scr::LoopAnim local.anim
	if (local.upperAnim != NULL)
		local.guy thread global/loopanim.scr::LoopUpperAnim local.upperAnim

	local.model flypath local.path local.speed local.speed 100

	// deleteOnEnd of 2 means don't delete, let the guy die by himself
	if (local.deleteOnEnd == 1)
		local.model waitmove
	else {
		local.model move
		// wait for the jeep to get past the far plane, then delete ourself
		while (vector_within local.model.origin $jeep.origin 1500)
			wait 0.2
	}
	local.model remove	
	local.guy remove
end

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

idleGuy local.modelNo local.path local.angle local.anim local.holster local.delay local.zAdj local.gun:
	$jeepRideStart waittill trigger	
	wait 0.1
	if (local.zAdj == NULL || local.zAdj == NIL)
		local.zAdj = 0
	if (local.delay == NULL || local.delay == NIL) {
		// wait until the jeep is within range
		while (!(vector_within $(local.path).origin $jeep.origin 1500))
			wait 0.2
	}
	else if (local.delay > 0)
		wait local.delay

	local.guy = spawn level.intro_model[local.modelNo]  
	//local.guy rendereffects "-shadow"
	local.guy notsolid
	local.guy physics_off
	local.guy.origin = $(local.path).origin
	local.guy.origin += ( 0 0 local.zAdj )
	local.guy.angles = ( 0 local.angle 0 )
	if (local.holster)
		local.guy holster
	else if (local.gun != NULL)
		local.guy gun local.gun
	local.guy thread global/loopanim.scr::LoopAnim local.anim

	// wait for the jeep to get past the far plane, then delete ourself
	wait 1
	while (vector_within local.guy.origin $jeep.origin 1500)
		wait 0.2

	local.guy remove
end

//-----------------------------------------------------------------------------

//-----------------------------------------------------------------------------

tankFollower local.modelNo local.guyNo local.path local.delay local.jogDelay local.stopDelay local.secondpath:
	$jeepRideStart waittill trigger	
	wait local.delay

	local.guy = spawn level.intro_model[local.modelNo]  
	//local.guy rendereffects "-shadow"
	local.guy notsolid
	local.guy physics_off
	local.guy.origin = $(local.path).origin
	local.guy gun thompson
	local.model = spawn script_model
	local.model.origin = $(local.path).origin
	local.model notsolid
	local.guy bind local.model
	local.guy dontdropweapons

	local.model flypath local.path 300 300 100

	local.guy.targetname = "intro_fodder_" + local.guyNo

	// run for a while
	local.guy thread global/loopanim.scr::LoopAnim thompson_run_forward
	local.model waitmove

	if (local.guyNo == 3) {
		local.guy damage $world 55000 $world (0 0 0) (0 0 0) (0 0 0) 0 9 0 0
		local.model remove
	}

	local.model flypath local.secondpath 220 220 100
//	local.guy thread global/loopanim.scr::LoopAnimStop
//	local.guy thread global/loopanim.scr::LoopAnim A_11_soldier_jog

	// 1 2 3 4 5 6
	// a b c f e b

	local.loopAnim = NULL
	local.diveAnim = NULL

	switch (local.guyNo) {
		case 1:
		case 6:
			local.model waitmove
			local.loopAnim = mg42_crouch_idle
			break

		case 4:
		case 2:
			local.model waitmove
			local.loopAnim = thompson_crouch_alert
			break

		case 3:
			local.model waitmove
			local.diveAnim = AA_trench_dive1
			local.loopAnim = thompson_crouch_alert
			// this is the poor bastard that Jones tells to get out the trench
			local.guy.enableEnemy = 0
			break

		case 5:
			local.model waitmove
			local.diveAnim = AA_trench_dive2
			local.loopAnim = mg42_crouch_idle
			break
	}

	if(local.model != NULL){local.model remove} //chrissstrahl - fix null entity reference
	local.guy physics_on
	local.guy solid
	local.guy thread global/loopanim.scr::LoopAnimStop
	local.guy anim thompson_stand_curious01
	local.guy turnto $frontLinePlayerRallyPoint

	if (local.diveAnim != NULL) {
		local.guy anim local.diveAnim
		local.guy waittill animdone
	}

	if (local.loopAnim != NULL)
		local.guy thread global/loopanim.scr::LoopAnim local.loopAnim

	//// then jog
	//wait local.jogDelay
	//local.guy thread global/loopanim.scr::LoopAnim A_11_soldier_jog
	//local.model modifyflypath 150 150 100

	//// then stop
	//local.guy physics_on
	//local.guy solid
	//local.model remove
	//local.guy thread global/loopanim.scr::LoopAnimStop
end

